/*
 * generated by Xtext 2.10.0
 */
package org.boomslang.dsl.mapping.validation

import org.boomslang.core.validation.PackageFolderStructureValidator
import org.boomslang.dsl.mapping.mapping.BMapping
import org.boomslang.dsl.mapping.mapping.MappingPackage
import com.wireframesketcher.model.Screen
import com.wireframesketcher.model.Widget
import org.eclipse.xtext.validation.Check
import org.eclipse.xtext.validation.ComposedChecks


/**
 * Custom validation rules. 
 * 
 * see http://www.eclipse.org/Xtext/documentation.html#validation
 */
@ComposedChecks(validators=#[PackageFolderStructureValidator])
class MappingValidator extends AbstractMappingValidator {

    val public static String WIDGET_NOT_MAPPED = "WIDGET_NOT_MAPPED"
    
    val public static String UNIQUE_CONSTRAINT_VIOLATION = "UNIQUE_CONSTRAINT_VIOLATION"

    @Check
    def allWidgetsMapped(BMapping mapping) {
        //Collect all mapped widgets
        val mappedWidgets = newArrayList()
        mapping.BWidgetMapping.forEach[mappedWidgets.add(it.widget)]
        //Check if all available widgets are in the list of mappedWidgets
        //Hint: Only widgets with the name attribute filled are checked
        mapping.screen.widgets.forEach [
            if (!isDuplicate && !mappedWidgets.contains(it) && it.name != null) {
                warning("Widget " + it.name +" "+it.type+ " not mapped", mapping, MappingPackage.Literals.BMAPPING__SCREEN,
                    WIDGET_NOT_MAPPED, it.name + ";" + it.type)
            }
        ]
    }
    
    @Check
    def checkForWidgetsWithSameName(BMapping mapping){
        mapping.screen.widgets.forEach [
            if(isDuplicate){
            warning("Widget " + it.name +" "+it.type+ " exists multiple times with the same name and type", mapping, MappingPackage.Literals.BMAPPING__SCREEN,
                    UNIQUE_CONSTRAINT_VIOLATION, it.name + ";" + it.type)
            }
        ]
    }
    
    def private isDuplicate(Widget widget){
        (widget.eContainer as Screen).widgets.filter[it.name==widget.name&&it.type==widget.type].size>1
    }
}
